plugins {
  id 'com.github.kt3k.coveralls' version '2.12.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

apply from: "$projectDir/gradle/testCoverage.gradle"

sourceCompatibility = 1.8

ext.moduleName = 'com.github.nocatch'

repositories {
  mavenCentral()
}

jar {
  inputs.property("moduleName", moduleName)
  manifest {
    attributes('Automatic-Module-Name': moduleName)
  }
}

test {
  useJUnitPlatform()
}

dependencies {
  testImplementation platform("org.junit:junit-bom:5.7.2")
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'org.hamcrest:hamcrest-library:2.1'
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier.set("sources")
}

publishing {
  repositories {
    maven {
      name = "OSSRH"
      setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
      credentials {
        username = System.getenv("OSSRH_USERNAME")
        password = System.getenv("OSSRH_TOKEN")
      }
    }
  }

  publications {
    maven(MavenPublication) {
      groupId = "io.github.micheljung"
      artifactId = "nocatch"

      from components.java
      artifact sourceJar

      pom {
        name = "NoCatch"
        description = project.description
        url = "https://github.com/micheljung/nocatch"
        licenses {
          license {
            name = "GNU General Public License, Version 3.0"
            url = "https://www.gnu.org/licenses/gpl-3.0.html"
          }
        }
        developers {
          developer {
            id = "mj"
            name = "Michel Jung"
            email = "michel.jung89@gmail.com"
          }
        }
        scm {
          connection = "scm:git:https://github.com/micheljung/nocatch.git"
          developerConnection = "scm:git:https://github.com/micheljung/nocatch.git"
          url = "https://github.com/micheljung/nocatch"
        }
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.maven
}
