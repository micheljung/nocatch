buildscript {
  dependencies {
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.12.0'
  }

  repositories {
    mavenCentral()
  }
}

apply plugin: 'java'
apply plugin: 'idea'

apply from: "$projectDir/gradle/testCoverage.gradle"

sourceCompatibility = 1.8

ext.moduleName = 'com.github.nocatch'

jar {
  inputs.property("moduleName", moduleName)
  manifest {
    attributes('Automatic-Module-Name': moduleName)
  }
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.11'
}

publishing {
  repositories {
    maven {
      name = "OSSRH"
      setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
      credentials {
        username = System.getenv("OSSRH_USERNAME")
        password = System.getenv("OSSRH_TOKEN")
      }
    }
  }

  publications {
    maven(MavenPublication) {
      groupId = "io.github.micheljung"
      artifactId = "nocatch"

      from components.java
      artifact javadoc.outputs.files

      pom {
        name = "NoCatch"
        description = project.description
        url = "https://github.com/micheljung/nocatch"
        licenses {
          license {
            name = "GNU General Public License, Version 3.0"
            url = "https://www.gnu.org/licenses/gpl-3.0.html"
          }
        }
        developers {
          developer {
            id = "mj"
            name = "Michel Jung"
            email = "michel.jung89@gmail.com"
          }
        }
        scm {
          connection = "scm:git:https://github.com/micheljung/nocatch.git"
          developerConnection = "scm:git:https://github.com/micheljung/nocatch.git"
          url = "https://github.com/micheljung/nocatch"
        }
      }
    }
  }
}

signing {
  useGpgCmd()
  sign(publishing.publications[publicationName])
}